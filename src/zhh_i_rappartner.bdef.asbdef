managed implementation in class zbp_hh_i_rappartner unique;
strict ( 2 );
with draft;

define behavior for ZHH_I_RAPPartner alias Partner
persistent table zhh_partner
draft table zhh_dpartner
lock master total etag LastChangedAt
authorization master ( instance )
late numbering
etag master LastChangedAt
{
  create;
  update;
  delete ( features : global );
  field ( readonly ) PartnerNumber;

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft determine action Prepare {
    validation validateIsFilled;
    validation validateCoreData;
  }

  validation validateIsFilled on save { create; }
  validation validateCoreData on save { create; field Country, PaymentCurrency; }

  determination fillCurrency on modify { create; update; }

  action ( features : instance ) fillEmptyStreets result [1] $self;
  static action clearAllEmptyStreets;
  factory action copyLine [1];
  static action withPopup parameter zhh_i_popupentity;

  mapping for zhh_partner
    {
      PartnerNumber   = partner;
      PartnerName     = name;
      Street          = street;
      City            = city;
      Country         = country;
      PaymentCurrency = payment_currency;
      LastChangedAt   = last_changed_at;
    }
}