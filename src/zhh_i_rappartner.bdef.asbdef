managed implementation in class zbp_hh_i_rappartner unique;
strict ( 2 );

define behavior for ZHH_I_RAPPartner alias Partner
persistent table zhh_partner
lock master
authorization master ( instance )
late numbering
//etag master <field_name>
{
  create;
  update;
  delete;
  field ( readonly ) PartnerNumber;

  validation validateIsFilled on save { create; }
  validation validateCoreData on save { create; field Country, PaymentCurrency; }

  determination fillCurrency on modify { create; update; }

  action fillEmptyStreets result [1] $self;
  static action clearAllEmptyStreets;
  factory action copyLine [1];
  static action withPopup parameter zhh_i_popupentity;

  mapping for zhh_partner
    {
      PartnerNumber   = partner;
      PartnerName     = name;
      Street          = street;
      City            = city;
      Country         = country;
      PaymentCurrency = payment_currency;
    }
}