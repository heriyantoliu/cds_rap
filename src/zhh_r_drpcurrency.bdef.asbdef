managed implementation in class zbp_hh_r_drpcurrency unique;
strict ( 2 );
with draft;

define behavior for ZHH_R_DRPCurrency alias Currency
with unmanaged save
draft table zhh_drp_currd
etag master LocalLastChanged
lock master total etag LastChanged
authorization master ( instance )
//etag master <field_name>
{
  internal create;
  update;
  internal delete;

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  field ( readonly ) Currency,
  Decimals,
  CurrencyISOCode,
  AlternativeCurrencyKey,
  CurrencyName,
  CurrencyShortName,
  LastEditor;

  action LoadExcelContent parameter ZHH_S_DRPExcelPopup;

  event AfterExcelLoad parameter ZHH_S_DRPEventData;

  mapping for zhh_drp_addcurr
    {
      Currency          = currency;
      CurrencyComment   = ccomment;
      Documentation     = documentation;
      PictureURL        = picture_url;
      ExcelAttachment   = excel_attachment;
      ExcelMimeType     = excel_mimetype;
      ExcelFilename     = excel_filename;
      PictureAttachment = picture_attachment;
      PictureMimeType   = picture_mimetype;
      PictureFilename   = picture_filename;
      LastEditor        = last_editor;
      LocalLastChanged  = local_last_changed;
      LastChanged       = last_changed;
    }

  association _Country { create; with draft; }
}

define behavior for ZHH_I_DRPCurrencyCountry alias Country
with unmanaged save
draft table zhh_drp_cound
lock dependent by _Currency
authorization dependent by _Currency
//etag master <field_name>
{
  update;
  delete;
  field ( readonly )
  Currency,
  CountryName;

  field ( readonly : update )
  Country;

  mapping for zhh_drp_country
    {
      Currency       = currency;
      Country        = country;
      CountryRanking = ranking;
    }
  association _Currency;
}